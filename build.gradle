plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.6.20'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'com.javafx'
version '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven{
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

ext {
    junitVersion = '5.8.2'
}

sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.javafx.intellijtheme'
    mainClass = 'com.javafx.intellijtheme.MyApp'
    applicationDefaultJvmArgs = ['--add-opens=javafx.graphics/javafx.scene=tornadofx','--add-opens=javafx.controls/javafx.scene.control=tornadofx']
}


[compileKotlin, compileTestKotlin].forEach {
    it.kotlinOptions {
        jvmTarget = '11'
    }
}


javafx {
    version = '16'
    modules = ['javafx.controls', 'javafx.fxml','javafx.graphics','javafx.web','javafx.media','javafx.swing',]
}

dependencies {
//    implementation 'eu.mlihosoft.monacofx:monacofx:0.0.8'

    implementation ('no.tornado:tornadofx:2.0.0-SNAPSHOT')
    implementation("org.apache.httpcomponents:httpclient:4.5.3")
    implementation("org.apache.felix:org.apache.felix.framework:6.0.1")
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-materialdesign2-pack:12.3.1'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}